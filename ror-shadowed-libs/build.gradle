/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */

plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "org.owasp.dependencycheck"       version "8.2.1"
    id "java"
    id "maven-publish"
}

repositories {
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url 'https://jitpack.io' }
}

dependencyCheck {
    /*
       Severity	Base Score
       None     0
       Low      0.1-3.9
       Medium   4.0-6.9
       High     7.0-8.9
       Critical 9.0-10.0
     */
    failBuildOnCVSS = 3
    suppressionFiles = ["${project.rootDir}/supressions_cve.xml"]
}

configurations {
    shadowCompile
    compile.extendsFrom(shadowCompile)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    shadowCompile group: 'com.fasterxml.jackson.dataformat',      name: 'jackson-dataformat-yaml',                version: '2.13.5'
    shadowCompile group: 'com.fasterxml.jackson.core',            name: 'jackson-databind',                       version: '2.13.5'
    shadowCompile group: 'org.yaml',                              name: 'snakeyaml',                              version: '2.0'
    shadowCompile group: 'com.jayway.jsonpath',                   name: 'json-path',                              version: '2.8.0'
}

shadowJar {
    configurations = [project.configurations.shadowCompile]
    exclude('META-INF/versions/9/module-info.class')
}

// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.named('shadowJar', ShadowJar) {
    enableRelocation true
    relocationPrefix "tech.beshu.ror"
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}