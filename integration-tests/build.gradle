buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath group: 'com.star-zero.gradle',                         name: 'githook',               version: '1.2.1'
        classpath group: 'gradle.plugin.com.github.maiflai',             name: 'gradle-scalatest',      version: '0.23'
        classpath group: 'gradle.plugin.com.hierynomus.gradle.plugins',  name: 'license-gradle-plugin', version: '0.15.0'
    }
}

apply plugin: 'com.github.maiflai.scalatest'
apply plugin: "com.github.hierynomus.license"
apply plugin: "com.star-zero.gradle.githook"
apply plugin: 'scala'

repositories {
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://artifacts.elastic.co/maven' }
}

compileScala {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

test {
    systemProperty "project.dir", rootProject.projectDir
    enabled = project.hasProperty('esModule')
    if (enabled) {
        def module = project.properties['esModule']
        systemProperty "esModule", module

        tags {
            exclude 'tech.beshu.tags.ExcludeESModule.' + module
        }

        suite 'tech.beshu.ror.integration.suites.*'
    }
    outputs.upToDateWhen { false }
    maxParallelForks = 1

    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

dependencies {
    testCompile project(':tests-utils')

    testCompile group: 'jakarta.xml.bind',              name: 'jakarta.xml.bind-api',       version: '2.3.2'
    testCompile group: 'org.glassfish.jaxb',            name: 'jaxb-runtime',               version: '2.3.2'
    testCompile group: 'ch.qos.logback',                name: 'logback-classic',            version: '1.2.3'
    testRuntime group: 'org.pegdown',                   name: 'pegdown',                    version: '1.4.2'
    testCompile group: 'org.scala-lang',                name: 'scala-library',              version: '2.12.4'
    testCompile group: 'org.scalatest',                 name: 'scalatest_2.12',             version: '3.1.0'
}

license {
    header rootProject.file('ReadonlyRESTLicenseHeader.txt')
    skipExistingHeaders true
    useDefaultMappings = false
    mapping {
        java = 'SLASHSTAR_STYLE'
        scala = 'SLASHSTAR_STYLE'
    }
}

githook {
    hooks {
        "pre-commit" {
            shell = "./gradlew license --rerun-tasks"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task ldapserver(type:JavaExec) {
   systemProperties = System.properties
   main = 'tech.beshu.ror.integration.LDAPServer'
   classpath = sourceSets.test.runtimeClasspath
}
