/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */
buildscript {
    ext {
        esVersion = project.properties['esVersion'] ?: "unknown"
    }
}

plugins {
    id "readonlyrest.base-common-conventions"

    id "com.github.johnrengelman.shadow"        version "8.1.1"
    id "com.github.maiflai.scalatest"           version "0.33"
}

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url 'https://jitpack.io' }
}

dependencyCheck {
    /*
       Severity	Base Score
       None     0
       Low      0.1-3.9
       Medium   4.0-6.9
       High     7.0-8.9
       Critical 9.0-10.0
     */
    failBuildOnCVSS = 3
    scanConfigurations = ['runtimeClasspath']
    suppressionFiles = ["${project.rootDir}/suppressions_cve.xml"]
}

sourceSets {
    test {
        resources {
            srcDirs += [
                    "$rootDir/tests-utils/src/main/resources"
            ]
        }
    }
}

test {
    systemProperty "project.dir", rootProject.projectDir
    enabled = project.hasProperty('esModule')
    if (enabled) {
        def module = project.properties['esModule']
        systemProperty "esModule", module
        tags {
            exclude 'tech.beshu.tags.ExcludeESModule.' + module
        }
        suite 'tech.beshu.ror.tools.*'
    }
    outputs.upToDateWhen { false }
    maxParallelForks = 1
}

configurations {
    shadowRuntime {
        extendsFrom runtimeClasspath
    }
}

dependencies {
    implementation project(path: ':ror-tools-core')
    implementation group: 'com.github.scopt',              name: 'scopt_3',                       version: '4.1.0'

    testImplementation(project(':tests-utils'))
    testImplementation group:  'ch.qos.logback',        name: 'logback-classic',  version: '1.3.14'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

compileScala {
    options.compilerArgs.addAll(
            "-feature",
            "-Xfatal-warnings",
            "-Wunused:implicits",
            "-Wunused:imports",
            "-Wunused:explicits",
            "-Wunused:strict-no-implicit-warn",
            "-Wunused:locals",
            "-Wunused:params",
            "-Wunused:privates",
            "-Wunused:nowarn",
    )
}

tasks.withType(ScalaCompile).configureEach {
    scalaCompileOptions.additionalParameters = ["-feature", "-Xfatal-warnings"]
}

shadowJar {
    minimize()
    manifest {
        attributes 'Main-Class': 'tech.beshu.ror.tools.RorToolsApp'
    }
    archiveFileName = "ror-tools.${getArchiveExtension().get()}"
    configurations = [project.configurations.shadowRuntime]
}

// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.named('shadowJar', ShadowJar) {
    enableRelocation true
    relocationPrefix "tech.beshu.ror.tools"
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

def netty4TransportArtifactName = "transport-netty4-" + esVersion + ".jar"
def netty4TransportArtifactDestinationPath = "org/elasticsearch/plugin/transport-netty4/" + esVersion + "/" + netty4TransportArtifactName

tasks.register('uploadArtifactsFromEsBinaries', Exec) {
    commandLine '../ci/upload-files-to-s3.sh', layout.buildDirectory.get().asFile.path + "/" + netty4TransportArtifactName, "build/" + netty4TransportArtifactDestinationPath
}

tasks.register('getArtifactsFromEsBinaries') {
    doLast {
        def esDir = new File(layout.buildDirectory.asFile.get(), "es-" + esVersion)
        def netty4TransportArtifactPath = esDir.path + "/elasticsearch-" + esVersion + "/modules/transport-netty4/" + netty4TransportArtifactName
        def netty4TransportDestPath = layout.buildDirectory
        project.logger.info("Coping " + netty4TransportArtifactPath + " to " + netty4TransportDestPath.get().asFile.path + " ...")
        copy {
            from netty4TransportArtifactPath
            into netty4TransportDestPath
        }
    }
}

uploadArtifactsFromEsBinaries.dependsOn getArtifactsFromEsBinaries

tasks.register('downloadEsBinaries') {
    doLast {
        def esTar = new File(layout.buildDirectory.asFile.get(), "es-" + esVersion + ".tar.gz")
        if (!esTar.exists()) {
            if (!layout.buildDirectory.asFile.get().exists()) {
                project.mkdir(layout.buildDirectory.asFile.get())
            }
            def esLinuxBinaries = "elasticsearch-" + esVersion + "-linux-x86_64.tar.gz"
            project.logger.info("Downloading " + esLinuxBinaries + " ...")
            new URL("https://artifacts.elastic.co/downloads/elasticsearch/" + esLinuxBinaries)
                    .withInputStream { i -> esTar.withOutputStream { it << i } }
        }
        def esDir = new File(layout.buildDirectory.asFile.get(), "es-" + esVersion)
        project.logger.info("Extracting " + esTar.path + " to " + esDir.path + "...")
        copy {
            from tarTree(esTar)
            into esDir
        }
    }
}

getArtifactsFromEsBinaries.dependsOn downloadEsBinaries

