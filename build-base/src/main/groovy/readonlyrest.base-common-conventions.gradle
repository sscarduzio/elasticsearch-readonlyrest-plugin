/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */

plugins {
    id "com.github.hierynomus.license"
    id "com.star-zero.gradle.githook"
    id "org.owasp.dependencycheck"
    id "maven-publish"
    id "scala"
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation group: 'org.scala-lang',     name: 'scala3-library_3',      version: '3.3.3'
}

tasks.withType(ScalaCompile).configureEach {
    scalaCompileOptions.forkOptions.with {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxMetaspaceSize=512m']
    }
}

test {
    reports {
        junitXml.getRequired().set(true)
        html.getRequired().set(false)
    }
}

dependencyCheck {
    /*
       Severity	Base Score
       None     0
       Low      0.1-3.9
       Medium   4.0-6.9
       High     7.0-8.9
       Critical 9.0-10.0
     */
    failBuildOnCVSS = 3
    scanConfigurations = ['runtimeClasspath'] 
    suppressionFiles = ["${project.rootDir}/suppressions_cve.xml"]
    
    analyzers {
        // We don't need 'exe' or 'dll' files scanning
        assemblyEnabled = false
        // Disable Maven Central checksum lookups (we already have Maven coords from Gradle).
        // Avoids flaky network calls to Central search.
        centralEnabled = false

        ossIndex {
            // Configure the OSS Index analyzer. If no credentials are supplied,
            // dependency-check will skip this analyzer and log a warning.
            if (System.getenv('OSS_INDEX_USERNAME')) {
                username = System.getenv('OSS_INDEX_USERNAME')
            }
            if (System.getenv('OSS_INDEX_PASSWORD')) {
                password = System.getenv('OSS_INDEX_PASSWORD')
            }
        }
    }
    data {
        if (System.getenv('DEPENDENCY_CHECK_DATA_DIR')) {  // We want to fall back to the default path for local builds
            directory = System.getenv('DEPENDENCY_CHECK_DATA_DIR')
        }
    }
    nvd {
        apiKey = System.getenv('NVD_API_KEY')
    }
}

githook {
    hooks {
        "pre-commit" {
            shell = "./gradlew license --rerun-tasks"
        }
    }
}

license {
    header rootProject.file('ReadonlyRESTLicenseHeader.txt')
    skipExistingHeaders true
    useDefaultMappings = false
    mapping {
        java = 'SLASHSTAR_STYLE'
        scala = 'SLASHSTAR_STYLE'
    }
}
