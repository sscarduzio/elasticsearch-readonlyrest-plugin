/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */

group = 'org.elasticsearch.plugin'
version = pluginVersion

buildscript {
    ext {
        pluginVersion = rootProject.properties['pluginVersion']
        s3BucketName = System.env.BUCKET ?: "readonlyrest-data"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath group: 'org.owasp',                                    name: 'dependency-check-gradle',  version: '6.5.3'
        classpath group: 'com.star-zero.gradle',                         name: 'githook',                  version: '1.2.1'
        classpath group: 'gradle.plugin.com.github.maiflai',             name: 'gradle-scalatest',         version: '0.24'
        classpath group: 'gradle.plugin.com.hierynomus.gradle.plugins',  name: 'license-gradle-plugin',    version: '0.15.0'
        classpath group: 'com.github.jengelman.gradle.plugins',          name: 'shadow',                   version: '4.0.4'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url 'https://jitpack.io' }
    maven {
        url = "s3://" + s3BucketName + "/build"
        authentication {
            awsIm(AwsImAuthentication)
        }
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "com.github.hierynomus.license"
apply plugin: 'com.github.maiflai.scalatest'
apply plugin: "com.star-zero.gradle.githook"
apply plugin: 'maven'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'scala'

dependencyCheck {
    /*
       Severity	Base Score
       None     0
       Low      0.1-3.9
       Medium   4.0-6.9
       High     7.0-8.9
       Critical 9.0-10.0
     */
    failBuildOnCVSS = 3
    scanConfigurations = ['runtime']
    suppressionFiles = ["${project.rootDir}/supressions_cve.xml"]
}

compileScala {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        events "standardOut", "started", "passed", "skipped", "failed"
    }
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

configurations {
    scalaCompilerPlugin{
        transitive = false
    }
    all*.exclude group: 'org.hdrhistogram', module: 'HdrHistogram'
    all*.exclude group: 'org.scala-lang', module: 'scala-compiler'
    all*.exclude group: 'dom4j', module: 'dom4j'
}
dependencies {
    compile project(':audit')
    compile project(path: ':ror-shadowed-libs', configuration: 'shadow')

    // todo: bring it back when ROR core is upgraded to scala 2.13
    scalaCompilerPlugin     "org.scalamacros:paradise_2.12.15:2.1.1"
//    compileClasspath        "com.github.ghik:silencer-lib_2.12.8:1.6.0"
//    testCompileClasspath    "com.github.ghik:silencer-lib_2.12.8:1.6.0"
//    scalaCompilerPlugin     "com.github.ghik:silencer-plugin_2.12.8:1.6.0"

    compile(group: 'com.softwaremill.sttp',                 name: 'async-http-client-backend-cats_2.12',    version: '1.7.2') {
        exclude group: 'io.netty', module: 'netty-tcnative-classes'
    }
    compile group: 'com.github.pathikrit',                  name: 'better-files_2.12',                      version: '3.8.0'
    compile group: 'io.circe',                              name: 'circe-core_2.12',                        version: '0.13.0'
    compile group: 'io.circe',                              name: 'circe-generic_2.12',                     version: '0.13.0'
    compile group: 'io.circe',                              name: 'circe-generic-extras_2.12',              version: '0.13.0'
    compile group: 'io.circe',                              name: 'circe-parser_2.12',                      version: '0.13.0'
    compile group: 'io.circe',                              name: 'circe-refined_2.12',                     version: '0.13.0'
    compile group: 'io.circe',                              name: 'circe-shapes_2.12',                      version: '0.13.0'
    compile group: 'commons-codec',                         name: 'commons-codec',                          version: '1.15'
    compile group: 'org.typelevel',                         name: 'cats-free_2.12',                         version: '2.1.1'
    compile group: 'com.softwaremill.sttp',                 name: 'core_2.12',                              version: '1.7.2'
    compile group: 'com.comcast',                           name: 'ip4s-cats_2.12',                         version: '1.1.1'
    compile group: 'org.typelevel',                         name: 'jawn-parser_2.12',                       version: '1.3.2'
    compile group: 'io.jsonwebtoken',                       name: 'jjwt-api',                               version: '0.11.2'
    compile group: 'io.jsonwebtoken',                       name: 'jjwt-impl',                              version: '0.11.2'
    compile group: 'io.jsonwebtoken',                       name: 'jjwt-orgjson',                           version: '0.11.2'
    compile group: 'org.jooq',                              name: 'joor-java-8',                            version: '0.9.13'
    compile group: 'org.apache.logging.log4j',              name: 'log4j-api-scala_2.12',                   version: '11.0'
    compile(group: 'org.apache.logging.log4j',              name: 'log4j-slf4j-impl',                       version: '2.11.2') {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
    }
    compile group: 'io.monix',                              name: 'monix_2.12',                             version: '3.0.0'
    compile group: 'eu.timepit',                            name: 'refined_2.12',                           version: '0.9.5'
    compile group: 'org.reflections',                       name: 'reflections',                            version: '0.9.11'
    compile group: 'com.github.blemale',                    name: 'scaffeine_2.12',                         version: '3.1.0'
    compile group: 'com.github.tototoshi',                  name: 'scala-csv_2.12',                         version: '1.3.6'
    compile group: 'org.scala-lang',                        name: 'scala-library',                          version: '2.12.15'
    compile group: 'org.scala-lang.modules',                name: 'scala-java8-compat_2.12',                version: '0.9.0'
    compile group: 'io.lemonlabs',                          name: 'scala-uri_2.12',                         version: '1.4.1'
    compile group: 'org.typelevel',                         name: 'squants_2.12',                           version: '1.4.0'
    compile group: 'com.unboundid',                         name: 'unboundid-ldapsdk',                      version: '6.0.6'
    compile group: 'com.lihaoyi',                           name: 'upickle_2.12',                           version: '0.7.1'
    // https://www.bouncycastle.org/latest_releases.html#1.0.2.4-NONCERT
    compile group: 'org.bouncycastle',                      name: 'bc-noncert',                             version: '1.0.2.4'
    compile(group: 'org.bouncycastle',                      name: 'bctls-fips',                             version: '1.0.14') {
        exclude group: 'org.bouncycastle', module: 'bc-fips'
    }
    compile(group: 'org.bouncycastle',                      name: 'bcpkix-fips',                            version: '1.0.7') {
        exclude group: 'org.bouncycastle', module: 'bc-fips'
    }

    testCompile project(':tests-utils')
    testCompile group: 'org.apache.logging.log4j',          name: 'log4j-core',                             version: '2.15.0'
    testRuntime group: 'ch.qos.logback',                    name: 'logback-classic',                        version: '1.2.3'
    testRuntime group: 'org.pegdown',                       name: 'pegdown',                                version: '1.4.2'
    testCompile group: 'com.typesafe.scala-logging',        name: 'scala-logging_2.12',                     version: '3.9.2'
    testCompile group: 'org.scalamock',                     name: 'scalamock_2.12',                         version: '5.1.0'
    testCompile group: 'org.scalatestplus',                 name: 'scalacheck-1-14_2.12',                   version: '3.1.0.0'
    testCompile group: 'org.scalatest',                     name: 'scalatest_2.12',                         version: '3.1.0'
    testCompile group: 'com.dimafeng',                      name: 'testcontainers-scala_2.12',              version: '0.39.8'

    constraints {
        compile group: 'io.netty',                          name: 'netty-codec-http2',                      version: '4.1.77.Final'
        compile group: 'io.netty',                          name: 'netty-handler-proxy',                    version: '4.1.77.Final'
        compile group: 'io.netty',                          name: 'netty-transport-native-unix-common',     version: '4.1.77.Final'
        compile group: 'io.netty',                          name: 'netty-transport-native-epoll',           version: '4.1.77.Final'
        compile group: 'io.netty',                          name: 'netty-transport-native-kqueue',          version: '4.1.77.Final'
        compile group: 'com.google.guava',                  name: 'guava',                                  version: '30.0-jre'
        compile group: 'io.spray',                          name: 'spray-json_2.12',                        version: '1.3.5'
        compile group: 'org.scala-lang',                    name: 'scala-reflect',                          version: '2.12.10'
        compile group: 'org.asynchttpclient',               name: 'async-http-client',                      version: '2.10.5'
    }
}

license {
    header rootProject.file('ReadonlyRESTLicenseHeader.txt')
    skipExistingHeaders true
    useDefaultMappings = false
    mapping {
        java = 'SLASHSTAR_STYLE'
        scala = 'SLASHSTAR_STYLE'
    }
}

githook {
    hooks {
        "pre-commit" {
            shell = "./gradlew license --rerun-tasks"
        }
    }
}

tasks.withType(ScalaCompile) {
    // Map plugin jars to -Xplugin parameter
    List<String> parameters =
            configurations.scalaCompilerPlugin.files.collect {
                '-Xplugin:'+ it.absolutePath
            }

    // Add whatever flags you typically add
    parameters += ["-Ypartial-unification", "-Ywarn-macros:after", "-feature"] //, "-Xfatal-warnings"]

    // Finally set the additionalParameters
    scalaCompileOptions.additionalParameters = parameters
}

// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "tech.beshu.ror"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

