/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */

group = 'org.elasticsearch.plugin'
version = pluginVersion

buildscript {
    ext {
        pluginVersion = rootProject.properties['pluginVersion']
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath 'gradle.plugin.com.github.maiflai:gradle-scalatest:0.23'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url 'https://jitpack.io' }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.maiflai.scalatest'
apply plugin: "com.github.hierynomus.license"
apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileScala {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        events "standardOut", "started", "passed", "skipped", "failed"
    }

    tags {
        exclude 'es70x'
    }
}

dependencies {
    compile project(':audit')
    compile project(path: ':ror-shadowed-libs', configuration: 'shadow')

    compile group: 'com.softwaremill.sttp',                 name: 'async-http-client-backend-cats_2.12',    version: '1.5.8'
    compile group: 'com.github.pathikrit',                  name: 'better-files_2.12',                      version: '3.8.0'
    compile group: 'io.circe',                              name: 'circe-core_2.12',                        version: '0.10.1'
    compile group: 'io.circe',                              name: 'circe-generic-extras_2.12',              version: '0.10.1'
    compile group: 'io.circe',                              name: 'circe-parser_2.12',                      version: '0.10.1'
    compile group: 'io.circe',                              name: 'circe-yaml_2.12',                        version: '0.9.0'
    compile group: 'commons-codec',                         name: 'commons-codec',                          version: '1.10'
    compile group: 'com.softwaremill.sttp',                 name: 'core_2.12',                              version: '1.5.1'
    compile group: 'com.comcast',                           name: 'ip4s-cats_2.12',                         version: '1.1.1'
    compile group: 'io.jsonwebtoken',                       name: 'jjwt-api',                               version: '0.10.5'
    compile group: 'io.jsonwebtoken',                       name: 'jjwt-impl',                              version: '0.10.5'
    compile group: 'io.jsonwebtoken',                       name: 'jjwt-jackson',                           version: '0.10.5'
    compile group: 'com.google.code.gson',                  name: 'gson',                                   version: '2.8.2'
    // todo: add MDC to logs
    compile group: 'org.apache.logging.log4j',              name: 'log4j-api-scala_2.12',                   version: '11.0'
    compile group: 'io.monix',                              name: 'monix_2.12',                             version: '3.0.0-RC2'
    compile('eu.timepit:refined_2.12:0.9.5') {
        exclude group: 'org.scala-lang', module: 'scala-compiler'
    }
    compile('org.reflections:reflections:0.9.11') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-simple'
        exclude group: 'dom4j', module: 'dom4j'
    }
    compile group: 'com.github.blemale',                    name: 'scaffeine_2.12',                         version: '2.5.0'
    compile group: 'org.scala-lang',                        name: 'scala-library',                          version: '2.12.4'
    compile group: 'org.scala-lang.modules',                name: 'scala-java8-compat_2.12',                version: '0.9.0'
    compile group: 'io.lemonlabs',                          name: 'scala-uri_2.12',                         version: '1.4.1'
    compile group: 'org.typelevel',                         name: 'squants_2.12',                           version: '1.4.0'
    compile group: 'com.unboundid',                         name: 'unboundid-ldapsdk',                      version: '4.0.9'
    
    testCompile group: 'org.apache.logging.log4j',          name: 'log4j-core',                             version: '2.11.2'
    testRuntime group: 'ch.qos.logback',                    name: 'logback-classic',                        version: '1.2.3'
    testRuntime group: 'org.pegdown',                       name: 'pegdown',                                version: '1.4.2'
    testCompile group: 'com.typesafe.scala-logging',        name: 'scala-logging_2.12',                     version: '3.9.2'
    testCompile group: 'org.scalamock',                     name: 'scalamock_2.12',                         version: '3.6.0'
    testCompile group: 'org.scalamock',                     name: 'scalamock-scalatest-support_2.12',       version: '3.6.0'
    testCompile group: 'org.scalatest',                     name: 'scalatest_2.12',                         version: '3.0.5'
    testCompile group: 'com.dimafeng',                      name: 'testcontainers-scala_2.12',              version: '0.25.0'

}

license {
    header rootProject.file('ReadonlyRESTLicenseHeader.txt')
    skipExistingHeaders true
    useDefaultMappings = false
    mapping {
        java = 'SLASHSTAR_STYLE'
        scala = 'SLASHSTAR_STYLE'
    }
}

compileScala {
    options.compilerArgs << "-Ywarn-macros:after"
}

// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "tech.beshu.ror"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

