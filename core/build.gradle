/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */

buildscript {
    ext {
        pluginVersion = rootProject.properties['pluginVersion']
        s3BucketName = System.env.BUCKET ?: "readonlyrest-data"
    }
}

plugins {
    id "readonlyrest.base-common-conventions"

    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.github.maiflai.scalatest" version "0.32"
    id "java-library"
}

group = 'org.elasticsearch.plugin'
version = pluginVersion

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url 'https://jitpack.io' }
    maven {
        url = "s3://" + s3BucketName + "/build"
        authentication {
            awsIm(AwsImAuthentication)
        }
        metadataSources {
            artifact()
        }
    }
}

dependencyCheck {
    /*
       Severity	Base Score
       None     0
       Low      0.1-3.9
       Medium   4.0-6.9
       High     7.0-8.9
       Critical 9.0-10.0
     */
    failBuildOnCVSS = 3
    scanConfigurations = ['runtimeClasspath']
    suppressionFiles = ["${project.rootDir}/supressions_cve.xml"]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    api project(path: ':audit', configuration: 'default')
    api project(path: ':ror-shadowed-libs', configuration: 'shadow')

    api group: 'org.asynchttpclient',           name: 'async-http-client',                   version: '2.10.5'
    api group: 'com.github.pathikrit',          name: 'better-files_3',                      version: '3.9.2'
    api group: 'io.circe',                      name: 'circe-core_3',                        version: '0.14.6'
    api group: 'io.circe',                      name: 'circe-generic_3',                     version: '0.14.6'
    api group: 'io.circe',                      name: 'circe-parser_3',                      version: '0.14.6'
    api group: 'io.circe',                      name: 'circe-refined_3',                     version: '0.14.6'
    api group: 'commons-codec',                 name: 'commons-codec',                       version: '1.15'
    api group: 'org.typelevel',                 name: 'cats-free_3',                         version: '2.10.0'
    api group: 'com.beachape',                  name: 'enumeratum_3',                        version: '1.7.3'
    api group: 'com.hrakaroo',                  name: 'glob',                                version: '0.9.0'
    api group: 'com.comcast',                   name: 'ip4s-core_3',                         version: '2.0.4'
    api group: 'org.typelevel',                 name: 'jawn-parser_3',                       version: '1.5.1'
    api group: 'io.jsonwebtoken',               name: 'jjwt-api',                            version: '0.11.5'
    api group: 'io.jsonwebtoken',               name: 'jjwt-impl',                           version: '0.11.5'
    api(group: 'io.jsonwebtoken',               name: 'jjwt-orgjson',                        version: '0.11.5') {
        // jjwt-orgjson:0.11.5 uses json version, which has known security vulnerabilities
        exclude group: 'org.json', module: 'json'
    }
    api group: 'org.jooq',                  name: 'joor-java-8',                            version: '0.9.13'
    api group: 'org.apache.logging.log4j',  name: 'log4j-api-scala_3',                      version: '13.1.0'
    api(group: 'org.apache.logging.log4j',  name: 'log4j-slf4j-impl',                       version: '2.11.2') {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
    }
    api group: 'io.monix',                      name: 'monix_3',                            version: '3.4.1'
    api group: 'eu.timepit',                    name: 'refined_3',                          version: '0.11.1'
    api group: 'org.reflections',               name: 'reflections',                        version: '0.9.11'
    api group: 'org.mozilla',                   name: 'rhino',                              version: '1.7.14'
    api group: 'com.github.ben-manes.caffeine', name: 'caffeine',                           version: '2.9.3'
    api group: 'com.github.tototoshi',          name: 'scala-csv_3',                        version: '1.3.10'
    api group: 'org.scala-lang.modules',        name: 'scala-parallel-collections_3',       version: '1.0.4'
    api group: 'io.lemonlabs',                  name: 'scala-uri_3',                        version: '4.0.3'
    api group: 'org.typelevel',                 name: 'squants_3',                          version: '1.8.3'
    api group: 'com.unboundid',                 name: 'unboundid-ldapsdk',                  version: '6.0.11'
    api group: 'com.lihaoyi',                   name: 'upickle_3',                          version: '3.3.0'
    // https://www.bouncycastle.org/latest_releases.html#1.0.2.4-NONCERT
    api group: 'org.bouncycastle',          name: 'bc-noncert',                             version: '1.0.2.4'
    api(group: 'org.bouncycastle',          name: 'bctls-fips',                             version: '1.0.19') {
        exclude group: 'org.bouncycastle', module: 'bc-fips'
    }
    api(group: 'org.bouncycastle',          name: 'bcpkix-fips',                            version: '1.0.7') {
        exclude group: 'org.bouncycastle', module: 'bc-fips'
    }

    testImplementation  project(':tests-utils')
    testImplementation  group: 'org.apache.logging.log4j',   name: 'log4j-core',                 version: '2.23.1'
    testRuntimeOnly     group: 'ch.qos.logback',             name: 'logback-classic',            version: '1.3.14'
    testRuntimeOnly     group: 'org.pegdown',                name: 'pegdown',                    version: '1.4.2'
    testImplementation  group: 'com.typesafe.scala-logging', name: 'scala-logging_3',            version: '3.9.5'
    testImplementation  group: 'org.scalamock',              name: 'scalamock_3',                version: '6.0.0'
    testImplementation  group: 'org.scalatestplus',          name: 'scalacheck-1-16_3',          version: '3.2.14.0'
    testImplementation  group: 'org.scalatest',              name: 'scalatest_3',                version: '3.2.16'
    testImplementation  group: 'com.dimafeng',               name: 'testcontainers-scala_3',     version: '0.41.4'

    constraints {
        api group: 'io.netty',              name: 'netty-codec-http2',                  version: '4.1.108.Final'
        api group: 'io.netty',              name: 'netty-handler-proxy',                version: '4.1.108.Final'
        api group: 'io.netty',              name: 'netty-transport-native-unix-common', version: '4.1.108.Final'
        api group: 'io.netty',              name: 'netty-transport-native-epoll',       version: '4.1.108.Final'
        api group: 'io.netty',              name: 'netty-transport-native-kqueue',      version: '4.1.108.Final'
        api group: 'com.google.guava',      name: 'guava',                              version: '32.0.1-jre'
        api group: 'io.spray',              name: 'spray-json_3',                       version: '1.3.6'
        api group: 'org.asynchttpclient',   name: 'async-http-client',                  version: '2.10.5'
    }
}

configurations {
    scalaCompilerPlugin {
        transitive = false
    }
    all*.exclude group: 'org.hdrhistogram', module: 'HdrHistogram'
    all*.exclude group: 'org.scala-lang',   module: 'scala-compiler'
    all*.exclude group: 'dom4j',            module: 'dom4j'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        events "standardOut", "started", "passed", "skipped", "failed"
    }
}

tasks.withType(ScalaCompile).configureEach {
    // Map plugin jars to -Xplugin parameter
    List<String> parameters =
            configurations.scalaCompilerPlugin.files.collect {
                '-Xplugin:' + it.absolutePath
            }

    // Add whatever flags you typically add
    parameters += [
            "-feature",
            "-Xfatal-warnings",
            "-Wunused:implicits",
            "-Wunused:imports",
            "-Wunused:explicits",
            "-Wunused:strict-no-implicit-warn",
            "-Wunused:locals",
            "-Wunused:params",
            "-Wunused:privates",
            "-Wunused:nowarn",
    ]

    parameters -= ["-target:8"]


    // Finally set the additionalParameters
    scalaCompileOptions.additionalParameters = parameters
}

// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.named('shadowJar', ShadowJar) {
    enableRelocation true
    relocationPrefix "tech.beshu.ror"
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
