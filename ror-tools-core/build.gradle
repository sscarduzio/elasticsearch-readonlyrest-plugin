/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */
plugins {
    id "readonlyrest.base-common-conventions"

    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.github.maiflai.scalatest"    version "0.33"
    id "java-library"
}

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    maven { url 'https://jitpack.io' }
}



test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
        events "standardOut", "started", "passed", "skipped", "failed"
    }
}

dependencies {
    api group: 'org.ow2.asm',                   name: 'asm',                              version: '9.8'
    api group: 'org.ow2.asm',                   name: 'asm-util',                         version: '9.8'
    api group: 'io.kevinlee',                   name: 'just-semver_3',                    version: '0.5.0'
    api group: 'com.lihaoyi',                   name: 'os-lib_3',                         version: '0.11.3'
    api group: 'com.github.pathikrit',          name: 'better-files_3',                   version: '3.9.2'
    api group: 'io.circe',                      name: 'circe-core_3',                     version: '0.14.6'
    api group: 'io.circe',                      name: 'circe-generic_3',                  version: '0.14.6'
    api group: 'io.circe',                      name: 'circe-parser_3',                   version: '0.14.6'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

compileScala {
    options.compilerArgs.addAll(
            "-feature",
            "-Xfatal-warnings",
            "-Wunused:implicits",
            "-Wunused:imports",
            "-Wunused:explicits",
            "-Wunused:strict-no-implicit-warn",
            "-Wunused:locals",
            "-Wunused:params",
            "-Wunused:privates",
            "-Wunused:nowarn",
    )
}

tasks.withType(ScalaCompile).configureEach {
    scalaCompileOptions.additionalParameters = ["-Xmax-inlines", "128", "-feature", "-Xfatal-warnings"]
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
}

// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.named('shadowJar', ShadowJar) {
    enableRelocation true
    relocationPrefix "tech.beshu.ror.tools"
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
