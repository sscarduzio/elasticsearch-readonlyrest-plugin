/*
 *    This file is part of ReadonlyREST.
 *
 *    ReadonlyREST is free software: you can redistribute it and/or modify
 *    it under the terms of the GNU General Public License as published by
 *    the Free Software Foundation, either version 3 of the License, or
 *    (at your option) any later version.
 *
 *    ReadonlyREST is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *    GNU General Public License for more details.
 *
 *    You should have received a copy of the GNU General Public License
 *    along with ReadonlyREST.  If not, see http://www.gnu.org/licenses/
 */

plugins {
    id "readonlyrest.plugin-common-conventions"
}

configurations {
    distJars {
        extendsFrom runtimeClasspath
        exclude group: 'org.bouncycastle'
    }
}

compileScala {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    implementation project(path: ':core', configuration: 'default')
    implementation project(path: ':es79x', configuration: 'default')

    implementation  group: 'org.elasticsearch.client',             name: 'elasticsearch-rest-high-level-client',   version: esVersion
    implementation  group: 'com.github.finagle',                   name: 'finchx-circe_2.12',                      version: '0.32.1'
    implementation  group: 'com.github.finagle',                   name: 'finchx-core_2.12',                       version: '0.32.1'
    implementation  group: 'org.javassist',                        name: 'javassist',                              version: '3.26.0-GA'
    implementation  group: 'org.jooq',                             name: 'joor-java-8',                            version: '0.9.13'
    implementation  group: 'org.codelibs.elasticsearch.module',    name: 'lang-mustache',                          version: esVersion
    implementation  group: 'org.apache.logging.log4j',             name: 'log4j-core',                             version: '2.15.0'
    implementation  group: 'org.codelibs.elasticsearch.module',    name: 'reindex',                                version: esVersion
}

license {
    header rootProject.file('BeshuLimitedLicenseHeader.txt')
    skipExistingHeaders true
    useDefaultMappings = false
    mapping {
        java = 'SLASHSTAR_STYLE'
        scala = 'SLASHSTAR_STYLE'
    }
}

tasks.register('rorproxy', Jar) {
    dependsOn(configureEsVersion, generateVersionsFile)

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Implementation-Title': 'ReadonlyREST ES Proxy',
                   'Implementation-Version': project.properties['pluginVersion'],
                   'Main-Class': 'tech.beshu.ror.proxy.Boot'
    }
    archivesBaseName = pluginName + '-proxy'
    from {
        configurations.distJars.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}